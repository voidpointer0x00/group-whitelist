import com.github.jengelman.gradle.plugins.shadow.tasks.ShadowJar

plugins {
    id 'java'
    id 'io.github.goooler.shadow' version '8.1.7'
    id 'io.freefair.lombok' version '8.6'
    id 'org.ajoberstar.grgit' version '5.2.2'
}

group = 'ru.voidpointer.spigot'
version = '1.1.0'
version_with_commit = grgit.head() != null ? "$version+${grgit.head().abbreviatedId}" : version

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven { url 'https://oss.sonatype.org/content/groups/public/' }
    maven { url 'https://repo.aikar.co/content/groups/aikar/' }
}

dependencies {
    //noinspection VulnerableLibrariesLocal
    compileOnly 'io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT'   /* minecraft server */
    compileOnly 'net.luckperms:api:5.4'                             /* permissions plugin */
    compileOnly 'org.spongepowered:configurate-yaml:4.1.2'          /* yaml configuration */
    compileOnly 'org.jetbrains:annotations:24.1.0'                  /* @NotNull etc. */
    compileOnly 'net.jcip:jcip-annotations:1.0'                     /* @ThreadSafe etc. */

    implementation 'co.aikar:acf-paper:0.5.1-SNAPSHOT' /* paper commands framework */

    testImplementation 'org.testng:testng:7.10.2' /* fuck JUnit */
}

test {
    useTestNG()
}

def targetJavaVersion = 21
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

shadowJar {
    archiveClassifier.unset()
}

tasks.named('shadowJar', ShadowJar) {
    enableRelocation = true
    relocationPrefix = 'voidpointer.spigot.groupwhitelist.libs'
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version_with_commit: version_with_commit]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}
